?start : program

program : header* statement*

?header : grid
grid : "grid" INT INT ";"

?statement : object ";"
?object : scene
        | image
        | tween

scene : "scene" identifier scene_element_list
scene_element_list : "{" scene_element ("," scene_element)* ","? "}"
scene_element : identifier NUMBER NUMBER

image : "image" identifier NUMBER NUMBER path

tween : "tween" identifier identifier frame_desc_list
frame_desc_list: "{" frame_desc ("," frame_desc)* ","? "}"
frame_desc : INT NUMBER NUMBER

?identifier : CNAME
?path : ESCAPED_STRING

COMMENT :  "/*" /(.|\n|\r)+/ "*/"
        |  "//" /(.)+/ NEWLINE

%ignore COMMENT

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.NEWLINE
%import common.INT
%import common.WS
%ignore WS
