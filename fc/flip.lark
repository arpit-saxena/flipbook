?start : program

program : header* statement*

?header : grid
grid : "grid" number number ";"

?statement : object ";"
?object : scene
        | image
        | tween

scene : "scene" identifier scene_element_list
scene_element_list : "{" scene_element ("," scene_element)* ","? "}"
scene_element : identifier number number

image : "image" identifier number number path

tween : "tween" identifier identifier frame_desc_list
frame_desc_list: "{" frame_desc ("," frame_desc)* ","? "}"
frame_desc : number number number

?identifier : CNAME
?number : INT
?path : ESCAPED_STRING

%import common.CNAME
%import common.ESCAPED_STRING
%import common.INT
%import common.WS
%ignore WS
